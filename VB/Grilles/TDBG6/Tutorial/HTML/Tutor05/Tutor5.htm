<HTML>
<HEAD>
<TITLE>True DBGrid Pro 6.0 (OLEDB)</TITLE>
</HEAD>
<BODY>
Tutorial 5 - Selecting Multiple Rows Using Bookmarks<P>

<OBJECT CLASSID="CLSID:BD96C556-65A3-11D0-983A-00C04FC29E33" ID="adcTutor" HEIGHT=1 WIDTH=1>
	<PARAM NAME="Server" VALUE="http://passat">
	<PARAM NAME="Connect" VALUE="DSN=TDBGDemo">
	<PARAM NAME="SQL" VALUE="select * from composer, opus, composer inner join opus on composer.last = opus.last">
</OBJECT>
<OBJECT id=tDBGrid CLASSID="clsid:0D6234D0-DBA2-11D1-B5DF-0060976089D0" WIDTH=600 HEIGHT=300>
	<PARAM NAME="DataSource" DATASRC="#adcTutor">
</OBJECT>
<P>
<BUTTON ID=btnSelect>Select</BUTTON>
<BUTTON ID=btnClear>Clear</BUTTON>
</BODY>
</HTML>
<SCRIPT LANGUAGE=VBSCRIPT>
Sub tDBGrid_DatasourceChanged()
' The following is necessary to work around a bug in ADO: the ADO Recordset
' Bookmark property has incorrect type and so it can't be passed to the grid
' (see btnSelect_OnClick, SelBks.Add dclone.Bookmark).
' The TDBGrid.BookmarkType property helps to avoid this bug. Setting it to
' the actual type of Recordset.Bookmark (it may vary in different situations,
' it is generally unpredictable) allows the grid to provide necessary fix-up.

	tDBGrid.BookmarkType = VarType(adcTutor.RecordSet.Bookmark)
End Sub

Sub btnSelect_OnClick()
' This routine loops through the Recordset to find and
' highlight all records with Country = "Germany"
    
	' We shall use a clone so that we do not move the actual
	' record position of the Data control
	Dim dclone
	Dim SelBks
	Dim Criteria
	Dim I
	Set dclone = adcTutor.Recordset.Clone()
	dclone.MoveFirst

	' For each matching record, add the bookmark to the
	' SelBookmarks collection of the grid.  The grid will
	' highlight the corresponding rows.  Note that the bookmarks
	' of a clone are compatible with the original set.
	' This is ONLY true of clones.
	Set SelBks = tDBGrid.SelBookmarks

	For I = 0 to dclone.RecordCount - 1
		If dclone.Fields.Item("Country") = "Germany" then
			SelBks.Add dclone.Bookmark
		End If
		dclone.MoveNext
	Next
End Sub

Sub btnClear_OnClick()
' Clear all selected rows by removing the selected records from
' the SelBookmarks collection.

	Dim SelBks
	Set SelBks = tDBGrid.SelBookmarks

	While SelBks.Count <> 0
		SelBks.Remove 0
	Wend
End Sub
</SCRIPT>